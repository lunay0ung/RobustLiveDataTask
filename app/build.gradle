apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.otamate.android.robustlivedatatask"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    if (project.hasProperty("ROBUST_LIVE_DATA_TASK_SIGNINGPROPS")) {
        signingConfigs {
            release {
                Properties properties = new Properties()
                properties.load(file(ROBUST_LIVE_DATA_TASK_SIGNINGPROPS).newDataInputStream())
                project.ext.set("RELEASE_STORE_FILE", properties.getProperty('RELEASE_STORE_FILE'))
                project.ext.set("RELEASE_STORE_PASSWORD", properties.getProperty('RELEASE_STORE_PASSWORD'))
                project.ext.set("RELEASE_KEY_ALIAS", properties.getProperty('RELEASE_KEY_ALIAS'))
                project.ext.set("RELEASE_KEY_PASSWORD", properties.getProperty('RELEASE_KEY_PASSWORD'))

                storeFile file(project.RELEASE_STORE_FILE)
                storePassword project.RELEASE_STORE_PASSWORD
                keyAlias project.RELEASE_KEY_ALIAS
                keyPassword project.RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            sourceSets.debug.resources.srcDirs = ['src/debug/res']
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("ROBUST_LIVE_DATA_TASK_SIGNINGPROPS")) {
                signingConfig signingConfigs.release
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "$appApkName-${variant.name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlinVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "android.arch.lifecycle:extensions:$archLifecycleExtensionsVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    testImplementation "junit:junit:$junitVersion"

    androidTestImplementation "com.android.support.test:runner:$supportTestRunnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$supportTestEspressoVersion"
}
